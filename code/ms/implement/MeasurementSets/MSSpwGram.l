/* -*- C -*-
    MSSpwGram.l: Lexical analyzer for ms selection commands
    Copyright (C) 1994,1995,1996,1997,1998,2001,2003
    Associated Universities, Inc. Washington DC, USA.

    This library is free software; you can redistribute it and/or modify it
    under the terms of the GNU Library General Public License as published by
    the Free Software Foundation; either version 2 of the License, or (at your
    option) any later version.

    This library is distributed in the hope that it will be useful, but WITHOUT
    ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
    FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Library General Public
    License for more details.

    You should have received a copy of the GNU Library General Public License
    along with this library; if not, write to the Free Software Foundation,
    Inc., 675 Massachusetts Ave, Cambridge, MA 02139, USA.

    Correspondence concerning AIPS++ should be addressed as follows:
           Internet email: aips2-request@nrao.edu.
           Postal address: AIPS++ Project Office
                           National Radio Astronomy Observatory
                           520 Edgemont Road
                           Charlottesville, VA 22903-2475 USA

    $Id: MSSpwGram.l,v 1.8 2006/09/28 07:11:05 sbhatnag Exp $
*/

%{
#undef YY_INPUT
#define YY_INPUT(buf,result,max_size) result=msSpwGramInput(buf,max_size)

#undef YY_DECL
#define YY_DECL int MSSpwGramlex (YYSTYPE* lvalp)
static string                qstr;
%}

WHITE     [ \t\n]*
DIGIT     [0-9]
INT       {DIGIT}+
EXP       [DdEe][+-]?{INT}
DOT       \.?
NUMBER    ({INT}|{INT}?{DOT}{INT}*)
FNUMBER   {NUMBER}?{EXP}?

KILO       ([Kk]?)
MEGA       (M?)
GIGA       (G?)
TERA       (T?)
HERTZ      (([Hh][Zz])?)
UNIT       (({KILO}|{MEGA}|{GIGA}|{TERA})?{HERTZ})

QSTRING   \"[^\"\n]*\"
STRING    ({QSTRING})+

QUOTE     (\")
RQUOTE    (\/)
NQ        [^\\\n\"]+
NRQ       [^\\\n\/]+

NAMES       ([a-zA-Z_{}]+[a-zA-Z0-9_{}]*)
IDENTIFIER  ({NAMES}+|STRING)
SIDENTIFIER  ({NAMES}+"*")

%x QS RS
/* rules */
%%
{QUOTE}   { // Start of a quoted string
            qstr.resize(0);
            BEGIN(QS);
          }

<QS>{NQ}  { 
            (qstr)+= MSSpwGramtext;
          }

<QS>{QUOTE} { /* saw closing quote - all done */
               BEGIN(INITIAL);
               lvalp->str = (char *)malloc((qstr.length() + 1)*sizeof(char));
               strcpy(lvalp->str,qstr.c_str());
               qstr.resize(0);

               return QSTRING;
             }

{RQUOTE}  { // Start of a regex string
            qstr.resize(0);
            BEGIN(RS);
          }

<RS>{NRQ} {
            (qstr)+= MSSpwGramtext;
          }

<RS>{RQUOTE} { /* saw closing quote - all done */
               BEGIN(INITIAL);
               lvalp->str = (char *)malloc((qstr.length() + 1)*sizeof(char));
               strcpy(lvalp->str,qstr.c_str());
               qstr.resize(0);

               return REGEX;
             }

{FNUMBER} {msSpwGramPosition() += yyleng;
            lvalp->str = (char *)malloc((strlen(MSSpwGramtext) + 1) * sizeof(char));
            strcpy(lvalp->str, MSSpwGramtext);

            return FNUMBER;
          } 
{UNIT}    { msSpwGramPosition() += yyleng;
            lvalp->str = MSSpwGramtext;

            return UNIT;
          }
"-"       { msSpwGramPosition() += yyleng;
            return DASH; }
","       { msSpwGramPosition() += yyleng;
            return COMMA;
          }
"<"       { msSpwGramPosition() += yyleng;
            return LT;
          }
">"       { msSpwGramPosition() += yyleng;
            return GT;
          }
"&"       { msSpwGramPosition() += yyleng;
            return AMPERSAND;
          }
"~"       { msSpwGramPosition() += yyleng;
            return TILDA;
          }
  /* Literals */

{IDENTIFIER} { msSpwGramPosition() += yyleng;
               lvalp->str = (char *)malloc((strlen(MSSpwGramtext) + 1) * sizeof(char));
               strcpy(lvalp->str, MSSpwGramtext);

               return IDENTIFIER;
             }
{SIDENTIFIER} { msSpwGramPosition() += yyleng;
                lvalp->str = (char *)malloc((strlen(MSSpwGramtext) + 1) * sizeof(char));
                strcpy(lvalp->str, MSSpwGramtext);
                return QSTRING;
              }
"("       { msSpwGramPosition() += yyleng; return LPAREN; }
")"       { msSpwGramPosition() += yyleng; return RPAREN;}
{WHITE}   { msSpwGramPosition() += yyleng;} /* Eat white spaces */
.         { msSpwGramPosition() += yyleng;return MSSpwGramtext[0];}
%%
